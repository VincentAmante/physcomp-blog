---
title: Calculator
---

The calculator utilises a button matrix, which would later provide some insight into how our final project's buttons worked. This one allows you to perform simple arithmetic calculations, of which are displayed in the Serial Monitor (Display not included).

## Demo
<video width="300" height="400" controls muted>
<source src="/videos/demo-calculator.mp4" type="video/mp4"/>
</video>

## Parts
- 4x4 Keypad
- 8 jumper wires
- Usual board and cable + power supply
![Parts](/images/parts-calculator.jpg)

## Instructions
- Connect each of the keypad's slots into the arduino
	- 4 wires will connect the keypad's rows
	- 4 wires will connect the keypad's columns
- Upload code below:

```c
#include <Keypad.h>
//https://www.cs.cmu.edu/~pattis/15-1XX/common/handouts/ascii.html

long first = 0;  
long second = 0;
double total = 0;

char customKey;
const byte ROWS = 4;
const byte COLS = 4;

char keys[ROWS][COLS] = {
  {'1','2','3','+'},
  {'4','5','6','-'},
  {'7','8','9','*'},
  {'C','0','=','/'}
};
byte rowPins[ROWS] = {2,3,4,5};
byte colPins[COLS] = {17,16,15,14};

Keypad customKeypad = Keypad( makeKeymap(keys), rowPins, colPins, ROWS, COLS); 

void setup()
{
  Serial.begin(9600);
}

void loop()
{
  customKey = customKeypad.getKey();
  switch(customKey) 
  {
  case '0' ... '9':
    first = first * 10 + (customKey - '0');
    Serial.println(first);
    break;

  case '+':
    first = (total != 0 ? total : first);
    Serial.println("+");
    second = SecondNumber();
    total = first + second;
    Serial.println(total);
    first = 0, second = 0;
    break;

  case '-':
    first = (total != 0 ? total : first);
    Serial.println("-");
    second = SecondNumber();
    total = first - second;
    Serial.println(total);
    first = 0, second = 0;
    break;

  case '*':
    first = (total != 0 ? total : first);
    Serial.println("*");
    second = SecondNumber();
    total = first * second;
    Serial.println(total);
    first = 0, second = 0;
    break;

  case '/':
    first = (total != 0 ? total : first);
    Serial.println("/");
    second = SecondNumber();

    second == 0 ? Serial.println("Invalid") : total = (float)first / (float)second;

    Serial.println(total);
    first = 0, second = 0;
    break;

  case 'C':
    total = 0;
    break;
  }
}

long SecondNumber()
{
  while( 1)
  {
    customKey = customKeypad.getKey();
    if(customKey >= '0' && customKey <= '9')
    {
      second = second * 10 + (customKey - '0');
      Serial.println(second);
    }

    if(customKey == '=') break;  //return second;
  }
 return second; 
}

```
